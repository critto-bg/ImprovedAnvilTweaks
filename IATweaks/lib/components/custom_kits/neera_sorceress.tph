/// Custom kits: makes Neera into a sorceress ///

SILENT

COPY_EXISTING ~neera8.cre~ ~Override/neera8.cre~
  WRITE_SHORT 0x24 4 // current HP
  WRITE_SHORT 0x26 4 // max HP
  WRITE_BYTE  0x234  1  // current level
  WRITE_BYTE  0x238 11 // strength
  WRITE_BYTE  0x239 0 // strength % bonus
  WRITE_BYTE  0x23a 10 // intelligence
  WRITE_BYTE  0x23b 18 // wisdom
  WRITE_BYTE  0x23c 14 // dexterity
  WRITE_BYTE  0x23d 16 // constitution
  WRITE_BYTE  0x23e 11 // charisma
  WRITE_LONG 0x244 0x40000000 // true class
  WRITE_BYTE  0x273 0x13 // class sorcerer

  REMOVE_KNOWN_SPELLS
  REMOVE_MEMORIZED_SPELLS

  ADD_KNOWN_SPELL ~SPWI112~ #0 ~wizard~
  ADD_KNOWN_SPELL ~SPWI124~ #0 ~wizard~

  READ_LONG  0x2a8 "mem_off"
  READ_LONG  0x2ac "mem_num"
  FOR (index = 0; index < mem_num; index = index + 1) BEGIN // searches through memorization tables
    READ_SHORT ("%mem_off%" + 0x0 + (0x10 * "%index%")) "spell_level"
    READ_SHORT ("%mem_off%" + 0x6 + (0x10 * "%index%")) "spell_type"

    PATCH_IF (spell_type = 1) BEGIN
      PATCH_MATCH %spell_level% WITH
      0
      BEGIN
        WRITE_SHORT ("%mem_off%" + 0x2 + (0x10 * "%index%")) 2 // write new amount
        WRITE_SHORT ("%mem_off%" + 0x4 + (0x10 * "%index%")) 2 // write new amount
      END

      DEFAULT
        WRITE_SHORT ("%mem_off%" + 0x2 + (0x10 * "%index%")) 0 // write new amount
        WRITE_SHORT ("%mem_off%" + 0x4 + (0x10 * "%index%")) 0 // write new amount
      END
    END
  END

  SPRINT ~opcode_to_delete~ ~233~
  LAUNCH_PATCH_MACRO ~DELETE_CRE_EFFECT~

  // staff
  LPF ~ADD_CRE_EFFECT~ INT_VAR
    opcode = 233
    timing = 9
    target = 1
    parameter1 = 1
    parameter2 = 102
  END

// Neera, ToB
COPY_EXISTING ~neera15.cre~ ~Override/neera15.cre~
  WRITE_SHORT 0x24 38 // current HP
  WRITE_SHORT 0x26 38 // max HP
  WRITE_BYTE  0x238 11 // strength
  WRITE_BYTE  0x239 0 // strength % bonus
  WRITE_BYTE  0x23a 10 // intelligence
  WRITE_BYTE  0x23b 18 // wisdom
  WRITE_BYTE  0x23c 14 // dexterity
  WRITE_BYTE  0x23d 16 // constitution
  WRITE_BYTE  0x23e 11 // charisma
  WRITE_LONG 0x244 0x00 // no kit
  WRITE_BYTE  0x273 0x13 // class sorcerer

  REMOVE_KNOWN_SPELLS
  REMOVE_MEMORIZED_SPELLS

  ADD_KNOWN_SPELL ~SPWI110~ #0 ~wizard~
  ADD_KNOWN_SPELL ~SPWI112~ #0 ~wizard~
  ADD_KNOWN_SPELL ~SPWI118~ #0 ~wizard~
  ADD_KNOWN_SPELL ~SPWI119~ #0 ~wizard~
  ADD_KNOWN_SPELL ~SPWI124~ #0 ~wizard~

  ADD_KNOWN_SPELL ~SPWI201~ #1 ~wizard~
  ADD_KNOWN_SPELL ~SPWI207~ #1 ~wizard~
  ADD_KNOWN_SPELL ~SPWI210~ #1 ~wizard~
  ADD_KNOWN_SPELL ~SPWI211~ #1 ~wizard~
  ADD_KNOWN_SPELL ~SPWI212~ #1 ~wizard~

  ADD_KNOWN_SPELL ~SPWI302~ #2 ~wizard~
  ADD_KNOWN_SPELL ~SPWI303~ #2 ~wizard~
  ADD_KNOWN_SPELL ~SPWI314~ #2 ~wizard~
  ADD_KNOWN_SPELL ~SPWI319~ #2 ~wizard~

  ADD_KNOWN_SPELL ~SPWI408~ #3 ~wizard~
  ADD_KNOWN_SPELL ~SPWI412~ #3 ~wizard~
  ADD_KNOWN_SPELL ~SPWI413~ #3 ~wizard~
  ADD_KNOWN_SPELL ~SPWI420~ #3 ~wizard~

  ADD_KNOWN_SPELL ~SPWI513~ #4 ~wizard~
  ADD_KNOWN_SPELL ~SPWI510~ #4 ~wizard~
  ADD_KNOWN_SPELL ~SPWI514~ #4 ~wizard~

  ADD_KNOWN_SPELL ~SPWI613~ #5 ~wizard~
  ADD_KNOWN_SPELL ~SPWI602~ #5 ~wizard~

  ADD_MEMORIZED_SPELL ~SPWI110~ #0 ~wizard~ ( 6 )
  ADD_MEMORIZED_SPELL ~SPWI112~ #0 ~wizard~ ( 6 )
  ADD_MEMORIZED_SPELL ~SPWI118~ #0 ~wizard~ ( 6 )
  ADD_MEMORIZED_SPELL ~SPWI119~ #0 ~wizard~ ( 6 )
  ADD_MEMORIZED_SPELL ~SPWI124~ #0 ~wizard~ ( 6 )

  ADD_MEMORIZED_SPELL ~SPWI201~ #1 ~wizard~ ( 6 )
  ADD_MEMORIZED_SPELL ~SPWI207~ #1 ~wizard~ ( 6 )
  ADD_MEMORIZED_SPELL ~SPWI210~ #1 ~wizard~ ( 6 )
  ADD_MEMORIZED_SPELL ~SPWI211~ #1 ~wizard~ ( 6 )
  ADD_MEMORIZED_SPELL ~SPWI212~ #1 ~wizard~ ( 6 )

  ADD_MEMORIZED_SPELL ~SPWI302~ #2 ~wizard~ ( 6 )
  ADD_MEMORIZED_SPELL ~SPWI303~ #2 ~wizard~ ( 6 )
  ADD_MEMORIZED_SPELL ~SPWI314~ #2 ~wizard~ ( 6 )
  ADD_MEMORIZED_SPELL ~SPWI319~ #2 ~wizard~ ( 6 )

  ADD_MEMORIZED_SPELL ~SPWI408~ #3 ~wizard~ ( 6 )
  ADD_MEMORIZED_SPELL ~SPWI412~ #3 ~wizard~ ( 6 )
  ADD_MEMORIZED_SPELL ~SPWI413~ #3 ~wizard~ ( 6 )
  ADD_MEMORIZED_SPELL ~SPWI420~ #3 ~wizard~ ( 6 )

  ADD_MEMORIZED_SPELL ~SPWI513~ #4 ~wizard~ ( 6 )
  ADD_MEMORIZED_SPELL ~SPWI510~ #4 ~wizard~ ( 6 )
  ADD_MEMORIZED_SPELL ~SPWI514~ #4 ~wizard~ ( 6 )

  ADD_MEMORIZED_SPELL ~SPWI613~ #5 ~wizard~ ( 4 )
  ADD_MEMORIZED_SPELL ~SPWI602~ #5 ~wizard~ ( 4 )

  READ_LONG  0x2a8 "mem_off"
  READ_LONG  0x2ac "mem_num"
  FOR (index = 0; index < mem_num; index = index + 1) BEGIN // searches through memorization tables
   READ_SHORT ("%mem_off%" + 0x0 + (0x10 * "%index%")) "spell_level"
   READ_SHORT ("%mem_off%" + 0x6 + (0x10 * "%index%")) "spell_type"

   PATCH_IF (spell_type = 1) BEGIN
     PATCH_MATCH %spell_level% WITH
       0 1 2 3 4
       BEGIN
         WRITE_SHORT ("%mem_off%" + 0x2 + (0x10 * "%index%")) 6 // write new amount
         WRITE_SHORT ("%mem_off%" + 0x4 + (0x10 * "%index%")) 6 // write new amount
       END

       5
       BEGIN
         WRITE_SHORT ("%mem_off%" + 0x2 + (0x10 * "%index%")) 4 // write new amount
         WRITE_SHORT ("%mem_off%" + 0x4 + (0x10 * "%index%")) 4 // write new amount
       END

       DEFAULT
         WRITE_SHORT ("%mem_off%" + 0x2 + (0x10 * "%index%")) 0 // write new amount
         WRITE_SHORT ("%mem_off%" + 0x4 + (0x10 * "%index%")) 0 // write new amount
     END
   END
  END

COPY_EXISTING ~neera8.cre~ ~Override/neera10.cre~

COPY_EXISTING ~neera8.cre~ ~Override/neera11.cre~

COPY_EXISTING ~neera8.cre~ ~Override/neera12.cre~

COPY_EXISTING ~neera8.cre~ ~Override/neera13.cre~
