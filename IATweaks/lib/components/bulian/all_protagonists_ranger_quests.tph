/// Remove class restrictions from The Shimmering Light and The Good, the Bad and the Ugly quest lines. The Ancient Secret remains Vagrant-only. ///

SILENT

// Shadow Temple, initiate ranger quests
COPY_EXISTING ~AR1401.BCS~ ~override\AR1401.BCS~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Class(Player1,RANGER_ALL)~ ~~
  END

// Shimmering Light, Umar's Cave
COPY_EXISTING ~AR1106.BCS~ ~override\AR1106.BCS~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Class(Player1,RANGER_ALL)~ ~~
  END

// Shimmering light, Ranger Cabin
COPY_EXISTING ~AR1107.BCS~ ~override\AR1107.BCS~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Class(Player1,RANGER_ALL)
  Global("Iarangerjob","GLOBAL",1)~ ~Global("Iarangerjob","GLOBAL",1)~
    REPLACE_TEXTUALLY ~Class(Player1,RANGER_ALL)
  Global("Iarangerjob","GLOBAL",5)~ ~Global("Iarangerjob","GLOBAL",5)~
  END

// Shimmering Light, Umar Hills
COPY_EXISTING ~AR1100.BCS~ ~override\AR1100.BCS~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Global("Iarangerjob","GLOBAL",2)[ %TAB%%LNL%%MNL%%WNL%]+Class(Player1,RANGER_ALL)~ ~Global("Iarangerjob","GLOBAL",2)~
    REPLACE_TEXTUALLY ~GlobalTimerExpired("Iad2appear","GLOBAL")[ %TAB%%LNL%%MNL%%WNL%]+Class(Player1,RANGER_ALL)~ ~GlobalTimerExpired("Iad2appear","GLOBAL")~
    REPLACE_TEXTUALLY ~Global("Iacreature2","AR1100",0)[ %TAB%%LNL%%MNL%%WNL%]+Class(Player1,RANGER_ALL)~ ~Global("Iacreature2","AR1100",0)~
    REPLACE_TEXTUALLY ~Global("Iacreature2","AR1100",1)[ %TAB%%LNL%%MNL%%WNL%]+Class(Player1,RANGER_ALL)~ ~Global("Iacreature2","AR1100",1)~
    REPLACE_TEXTUALLY ~GlobalTimerExpired("Iad3appear","GLOBAL")[ %TAB%%LNL%%MNL%%WNL%]+Class(Player1,RANGER_ALL)~ ~GlobalTimerExpired("Iad3appear","GLOBAL")~
    REPLACE_TEXTUALLY ~Global("Iacreature2","AR1100",1)[ %TAB%%LNL%%MNL%%WNL%]+Class(Player1,RANGER_ALL)~ ~Global("Iacreature2","AR1100",1)~
    REPLACE_TEXTUALLY ~Global("Iacreature2","AR1100",2)[ %TAB%%LNL%%MNL%%WNL%]+Class(Player1,RANGER_ALL)~ ~Global("Iacreature2","AR1100",2)~
    REPLACE_TEXTUALLY ~GlobalTimerExpired("Iaauriel","GLOBAL")[ %TAB%%LNL%%MNL%%WNL%]+Class(Player1,RANGER_ALL)~ ~GlobalTimerExpired("Iaauriel","GLOBAL")~
  END

// Guildmaster of Assassins note, AI script
COPY_EXISTING ~S!rune1.BCS~ ~override\S!rune1.BCS~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Class(Player1,RANGER_ALL)~ ~~
  END

// Grandfather of Assassins note in the sewers beneath Copper Coronet
COPY_EXISTING ~AR0404.BCS~ ~override\AR0404.BCS~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Class(Player1,RANGER_ALL)~ ~~
  END

// Grandfather of Assassins note in the sewers beneath Copper Coronet
COPY_EXISTING ~BALDUR.BCS~ ~override\BALDUR.BCS~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~!Class(Player1,RANGER_ALL)~ ~placeholder~
    REPLACE_TEXTUALLY ~Class(Player1,RANGER_ALL)~ ~~
    REPLACE_TEXTUALLY ~placeholder~ ~!Class(Player1,RANGER_ALL)~
  END

// Good, Bad n' Ugly, ranger cabin
COPY_EXISTING ~AR1107.BCS~ ~override\AR1107.BCS~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Class(Player1,RANGER_ALL)
  !Class(Player1,CLERIC_ALL)
  Kit(Player1,VAGRANT)
  GlobalGT("Iarangerjob","GLOBAL",11)~ ~GlobalGT("Iarangerjob","GLOBAL",11)~
  END

// Good, Bad n' Ugly, the Bridge encounter with Malbar
COPY_EXISTING ~AR0500.BCS~ ~override\AR0500.BCS~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Kit(Player1,VAGRANT)~ ~~
  END

// Good, Bad n' Ugly, the inn encounter with Diviner
COPY_EXISTING ~AR0511.BCS~ ~override\AR0511.BCS~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Class(Player1,RANGER_ALL)
  Kit(Player1,VAGRANT)~ ~~
  END

// Good, Bad n' Ugly, Umar Hills
COPY_EXISTING ~AR1100.BCS~ ~override\AR1100.BCS~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Kit(Player1,VAGRANT)~ ~~
  END

// Good, Bad n' Ugly, Mekrath's lair
COPY_EXISTING ~AR0705.BCS~ ~override\AR0705.BCS~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY EVALUATE_REGEXP ~OR(2)[ %TAB%%LNL%%MNL%%WNL%]+GlobalLT("Iaswanmejob","GLOBAL",5)[ %TAB%%LNL%%MNL%%WNL%]+!Kit(Player1,VAGRANT)~ ~GlobalLT("Iaswanmejob","GLOBAL",5)~
  END

// Good, Bad n' Ugly, Diviner's hideout
COPY_EXISTING ~ARia18.BCS~ ~override\ARia18.BCS~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Class(Player1,RANGER_ALL)
  Kit(Player1,VAGRANT)~ ~~
  END
