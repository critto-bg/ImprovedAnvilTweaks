/// The component simplifies the in-game economy by replacing lots of loot with its selling price in gold ///

// set up the arrays //

PRINT "Setting up..."

SILENT

COPY - "IATweaks/lib/tables/economy_containers_items.2da" "inlined/economy_containers_items.2da"
  READ_2DA_ENTRIES_NOW rows 3

  FOR (num = 1; num < rows; num += 1) BEGIN
    READ_2DA_ENTRY_FORMER rows num 0 area
    READ_2DA_ENTRY_FORMER rows num 1 item_res
    READ_2DA_ENTRY_FORMER rows num 2 gold

    SPRINT $economy_container_items("%area%" "%item_res%" "%gold%") "%num%"
  END

ACTION_CLEAR_ARRAY rows

COPY - "IATweaks/lib/tables/economy_creatures_items.2da" "inlined/economy_creatures_items.2da"
  READ_2DA_ENTRIES_NOW rows 2

  FOR (num = 1; num < rows; num += 1) BEGIN
    READ_2DA_ENTRY_FORMER rows num 0 cre_res
    READ_2DA_ENTRY_FORMER rows num 1 item_res

    SPRINT $economy_creatures_items("%cre_res%" "%item_res%") "%num%"
  END

ACTION_CLEAR_ARRAY rows

COPY - "IATweaks/lib/tables/economy_creatures_gold.2da" "inlined/economy_creatures_gold.2da"
  READ_2DA_ENTRIES_NOW rows 2

  FOR (num = 1; num < rows; num += 1) BEGIN
    READ_2DA_ENTRY_FORMER rows num 0 cre_res
    READ_2DA_ENTRY_FORMER rows num 1 gold

    SPRINT $economy_creatures_gold("%cre_res%" "%gold%") "%num%"
  END

// replace the items //

PRINT "Replacing items in the containers with gold..."

SILENT

ACTION_PHP_EACH economy_container_items AS record => int BEGIN
  COPY_EXISTING ~%record_0%.are~ ~override/%record_0%.are~
    LAUNCH_PATCH_FUNCTION ~REPLACE_AREA_ITEM~
      INT_VAR
        charges1 = record_2
      STR_VAR
        old_item = EVALUATE_BUFFER "%record_1%"
        new_item = ~MISC07~
    END

    BUT_ONLY
END

PRINT "Replacing items on creatures with gold..."

SILENT

ACTION_PHP_EACH economy_creatures_items AS record => int BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%record_0%.cre~ BEGIN
    COPY_EXISTING ~%record_0%.cre~ ~override/%record_0%.cre~
      GET_OFFSET_ARRAY items CRE_V10_ITEMS

      PHP_EACH items AS int => items_off BEGIN
        READ_ASCII items_off item (8) NULL

        PATCH_IF ("%item%" STRING_COMPARE_CASE "%record_1%" = 0) BEGIN // find the item
          READ_BYTE 0x010 + items_off itemflags
          WRITE_BYTE 0x10 + items_off (itemflags BOR 0b00001010) // set Unstealable and undroppable flags
        END
      END

      BUT_ONLY
  END
END

ACTION_PHP_EACH economy_creatures_gold AS record => int BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%record_0%.cre~ BEGIN
    COPY_EXISTING ~%record_0%.cre~ ~override/%record_0%.cre~
      ADD_CRE_ITEM ~MISC07~ (~%record_1%~) #0 #0 ~UNSTEALABLE~ ~INV~

      BUT_ONLY
  END
END

// do the cosmetics //

// text lines inside the translation files are numbered according to the original numbers from cespy.tra / crom.tra in the main mod
PRINT "Updating dialogues..."

SILENT

COMPILE ~IATweaks/resources/dlg/crom.simplified.d~
  USING ~%LANGUAGE%crom.simplified.tra~

COMPILE ~IATweaks/resources/dlg/cespy.simplified.d~
  USING ~%LANGUAGE%cespy.simplified.tra~

// add some stuff back manually for convenience //

PRINT "A few last changes..."

SILENT

// add some adamantine items to the "exit from underdark" area
COPY_EXISTING ~AR2401.are~ ~override/AR2401.are~
  PATCH_FOR_EACH ~item_code~ IN ~dwplat01~ ~dwshld01~ ~dwblun01~ ~dwsw1h02~ ~dwxbow01~ ~dwsper01~ BEGIN
    LPF ADD_AREA_ITEM
      STR_VAR
        item_to_add = EVALUATE_BUFFER ~%item_code%~
        container_to_add_to = ~1~
        charges1 = ~1~
    END
  END

  BUT_ONLY

// add some crafting ingredients for sale to Roger the Fence
COPY_EXISTING ~roger.sto~ ~override/roger.sto~
  ADD_STORE_ITEM ~amul09~ LAST #0 #0 #0 ~IDENTIFIED&UNSTEALABLE~ #3
  ADD_STORE_ITEM ~misc42~ #1 #1 #1 ~IDENTIFIED&UNSTEALABLE~ #5
